buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile).tap {
            configureEach {
                options.compilerArgs << "-Xmaxerrs" << "5000"
                options.compilerArgs << "-Xlint:deprecation"
            }
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'pdfium-android'

    publishedGroupId = 'com.github.barteksc'
    libraryName = 'PdfiumAndroid'
    artifact = 'pdfium-android'

    libraryDescription = 'Fork of library for rendering PDFs on Android\'s Surface or Bitmap'

    siteUrl = 'https://github.com/barteksc/PdfiumAndroid'
    gitUrl = 'https://github.com/barteksc/PdfiumAndroid.git'

    libraryVersion = '1.9.0'

    developerId = 'barteksc'
    developerName = 'Bartosz Schiller'
    developerEmail = 'barteksch@boo.pl'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    namespace 'com.shockwave.pdfium'
    compileSdk = 35
    defaultConfig {
        minSdk = 21
        targetSdk = 35

        ndkVersion '29.0.14033849'
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17 -fexceptions -frtti"
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    publishing {
        singleVariant("release") {}
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/cpp/lib']
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://andob.io/repository/open_source/' }
}

project.afterEvaluate {
    publishing {
        publications {
            repositories.maven {
                url 'https://andob.io/repository/open_source/'
                credentials {
                    username "$System.env.MAVEN_PUBLISH_USERNAME"
                    password "$System.env.MAVEN_PUBLISH_PASSWORD"
                }
            }

            release(MavenPublication) {
                from components.release
                groupId 'ro.andob.pdfium-android'
                artifactId 'pdfium-android'
                version '1.1.4'
            }
        }
    }
}
